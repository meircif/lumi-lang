~~~ TL5 compiler - Constant expressions ~~~
module tl5-compiler

~~~ 
Convert text to number based on given `int-base`.
Return whether conversion succeeded
~~~
func get-number-value(user String text, copy Int int-base)->(
        var Int value, var Bool is-number)
    is-number := true
    value := 0
    var Bool is-hex(copy int-base = 16)
    for n in text.length
        value := value * int-base
        var Char ch(copy text[n])
        if is-hex
            if ch >= 'a' and ch <= 'f'
                value += ch - 'a' + 10
            else-if ch >= 'A' and ch <= 'F'
                value += ch - 'A' + 10
            else
                if ch < '0' or ch > '9'
                    is-number := false
                    return
                value += ch - '0'
        else
            if ch < '0' or ch > '0' + int-base - 1
                is-number := false
                return
            value += ch - '0'


~~~ A basic expression that has a single textual value ~~~
class TextExpression(Expression)
    owner String text
    
    func dynamic write()
        write(user self.text)


~~~ Constant integer expression ~~~
class IntExpression(Expression)
    var Int value
    
    new(user SyntaxTreeCode code-node, copy Int value)
        base(user code-node)
        self.value := value
        self.set-simple-type(user glob.type-int)
        self.access := Access.VAR
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new IntExpression int-expression(user code-node, copy 0)
        int-expression.parse(owner text)
        expression := int-expression
    
    ~~~ parsing `123` or `-123` or `0b10011` or `0x3d4f` ~~~
    func inst parse(owner String text)
        user String number(user text)
        var Bool is-negative(copy number[0] = '-')
        if is-negative
            number := number[1:number.length - 1]
        if number[0] = '0' and number.length > 1
            var Char second(copy number[1])
            if second >= '0' and second <= '7'
                if not get-number-value(user number, copy 8)->(var self.value)
                    self.syntax-error(user "illegal octal number", user text)
            else
                number := number[2:number.length - 2]
                if second = 'b' or second = 'B'
                    if not get-number-value(user number, copy 2)->(var self.value)
                        self.syntax-error(user "illegal binary number", user text)
                else-if second = 'x' or second = 'X'
                    if not get-number-value(user number, copy 16)->(var self.value)
                        self.syntax-error(user "illegal hexadecimal number", user text)
                else
                    self.syntax-error(user "illegal number", user text)
        else-if not get-number-value(user number, copy 10)->(var self.value)
            self.syntax-error(user "illegal number", user text)
        if is-negative
            self.value := - self.value
        text := _
    
    func dynamic get-constant-value()->(var Int value, var Bool has-value)
        value := self.value
        has-value := true
        
    func dynamic write()
        write-int(copy self.value)


~~~ Constant character expression ~~~
class CharExpression(TextExpression)
    var Char value
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new CharExpression char-expression(user code-node)
        char-expression.parse(owner text)
        expression := char-expression
    
    ~~~ parsing `'a'` or `'\n'` or `'\xf5'` or `'\270'` ~~~
    func inst parse(owner String text)
        var Char ch
        if text.length = 3
            self.value := text[1]
            if self.value = '\'' or self.value = '\\'
                self.syntax-error(user "illegal character constant", user text)
        else-if text.length = 4
            if text[1] != '\\'
                self.syntax-error(user "illegal character constant", user text)
            if not self.char-from-escape(copy text[2])->(var self.value)
                self.syntax-error(user "illegal character constant", user text)
        else-if text.length = 6
            if text[1] != '\\'
                self.syntax-error(user "illegal character constant", user text)
            ch := text[2]
            var Int value
            if ch = 'x'
                if not get-number-value(user text[3:2], copy 16)->(var value)
                    self.syntax-error(user "illegal character constant", user text)
            else
                if not get-number-value(user text[2:3], copy 8)->(var value)
                    self.syntax-error(user "illegal character constant", user text)
            self.value := value
        else
            self.syntax-error(user "illegal character constant", user text)
        if text[text.length - 1] != '\''
            self.syntax-error(user "illegal character constant", user text)
        self.set-simple-type(user glob.type-char)
        self.access := Access.VAR
        self.text := text
        
    func inst char-from-escape(copy Char escape)->(
            var Char value, var Bool is-legal)
        user String escapes(user "'\"?\\abfnrtv0")
        user String values(user "\'\"\?\\\a\b\f\n\r\t\v\0")
        for n in escapes.length
            if escape = escapes[n]
                value := values[n]
                is-legal := true
                return
        is-legal := false
    
    func dynamic get-constant-value()->(var Int value, var Bool has-value)
        value := self.value
        has-value := true


~~~ Constant string expression ~~~
class StringExpression(TextExpression)
    owner VariableExpression aux-variable
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new StringExpression string-expression(user code-node)
        string-expression.parse(owner text)
        expression := string-expression
    
    ~~~ parsing `"some string"` ~~~
    func inst parse(owner String text)
        self.check-string(user text)
        self.set-simple-type(user glob.type-string)
        self.access := Access.VAR
        self.is-var := true
        self.text := text
        self.strip-multilines()
    
    func inst strip-multilines()
        var Int index-write(copy 1)
        for index-read in 1:self.text.length - 1
            if self.text[index-read] = '\n'
                var Bool skip-new-line
                if index-read > 0
                    skip-new-line := self.text[index-read - 1] = '\\'
                var Int expected(copy self.code-node.parent.indentation-spaces +
                        (2 * INDENTATION-SPACES))
                var Int indentation(copy 0)
                do
                    index-read += 1
                    while index-read < self.text.length - 1
                    while self.text[index-read] = ' '
                    indentation += 1
                    while indentation < expected
                if indentation < expected
                    self.code-node.syntax-error-indentation(
                            copy indentation, copy expected)
                if skip-new-line
                    index-write -= 1
                else
                    self.text[index-write] := '\\'
                    index-write += 1
                    self.text[index-write] := 'n'
                    index-write += 1
            else
                self.text[index-write] := self.text[index-read]
                index-write += 1
        if index-write < self.text.length - 1
            self.text[index-write] := '"'
            index-write += 1
            self.text[index-write] := '\0'
            self.text.length := index-write
    
    func dynamic analyze()
        self.code-node.add-aux-variable(
                copy Access.VAR, copy true, user self.result-type)->(
                owner self.aux-variable)
    
    func dynamic write-preactions()
        ; INIT_STRING_CONST(`line`, `cleanup`, `symbol`, `text`)
        write(user "INIT_STRING_CONST(")
        self.write-line-num()
        write(user ", ")
        self.code-node.parent.write-cleanup-label()
        write(user ", ")
        self.aux-variable.write()
        write(user ", ")
        write(user self.text)
        write(user ");\n")
        self.code-node.write-spaces()
    
    func dynamic write()
        self.aux-variable.write()


~~~ Constant empty expression `_` ~~~
class EmptyExpression(Expression)
    new(user SyntaxTreeCode code-node)
        base(user code-node)
        self.set-simple-type(user glob.type-empty)
        self.access := Access.STRONG
    
    func dynamic write()
        write(user "NULL")
    
    func dynamic write-dynamic()
        self.write()
    
    func dynamic write-refman()
        self.write()
