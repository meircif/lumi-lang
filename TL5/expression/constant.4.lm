~~~ TL5 compiler - Constant expressions ~~~
module tl5-compiler

~~~ ~
Convert text to number based on given `int-base`.
Return whether conversion succeeded
~~~
func get-number-value(user String text, copy Int int-base)->(
        owner Long value, var Bool is-number)
    is-number := true
    value := Long()
    var Bool is-hex(copy int-base = 16)
    for n in text.length
        value.mul(copy int-base)
        var Char ch(copy text[n])
        if is-hex
            if ch >= 'a' and ch <= 'f'
                value.add(copy ch - 'a' + 10)
            else-if ch >= 'A' and ch <= 'F'
                value.add(copy ch - 'A' + 10)
            else
                if ch < '0' or ch > '9'
                    is-number := false
                    return
                value.add(copy ch - '0')
        else
            if ch < '0' or ch > '0' + int-base - 1
                is-number := false
                return
            value.add(copy ch - '0')


~~~ A basic expression that has a single textual value ~~~
class TextExpression(Expression)
    owner String text
    
    func dynamic write()
        write(user self.text)


~~~ Constant integer expression ~~~
class IntExpression(Expression)
    owner Long value
    
    new(user SyntaxTreeCode code-node, copy Int value)
        base(user code-node)
        self.value := Long()
        self.value.set(copy value)
        self.set-simple-type(user glob.type-int)
        self.access := Access.VAR
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new IntExpression int-expression(user code-node, copy 0)
        int-expression.parse(owner text)
        expression := int-expression
    
    ~~~ parsing `123` or `-123` or `0b10011` or `0x3d4f` ~~~
    func inst parse(owner String text)
        user String number(user text)
        var Bool is-negative(copy number[0] = '-')
        if is-negative
            number := number[1:number.length - 1]
        if number[0] = '0' and number.length > 1
            var Char second(copy number[1])
            if second >= '0' and second <= '7'
                if not get-number-value(user number, copy 8)->(
                        owner self.value)
                    self.syntax-error(user "illegal octal number", user text)
            else
                number := number[2:number.length - 2]
                if second = 'b' or second = 'B'
                    if not get-number-value(user number, copy 2)->(
                            owner self.value)
                        self.syntax-error(
                                user "illegal binary number", user text)
                else-if second = 'x' or second = 'X'
                    if not get-number-value(user number, copy 16)->(
                            owner self.value)
                        self.syntax-error(
                                user "illegal hexadecimal number", user text)
                else
                    self.syntax-error(user "illegal number", user text)
        else-if not get-number-value(user number, copy 10)->(owner self.value)
            self.syntax-error(user "illegal number", user text)
        if is-negative
            self.value.mul(copy -1)
        text := _
    
    func dynamic get-constant-value()->(owner Long value, var Bool has-value)
        self.value.copy()->(owner value)
        has-value := true
        
    func dynamic write()
        write-long(user self.value)


~~~ Constant character expression ~~~
class CharExpression(TextExpression)
    var Char value
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new CharExpression char-expression(user code-node)
        char-expression.parse(owner text)
        expression := char-expression
    
    ~~~ parsing `'a'` or `'\n'` or `'\xf5'` or `'\270'` ~~~
    func inst parse(owner String text)
        var Char ch
        if text.length = 3
            self.value := text[1]
            if self.value = '\'' or self.value = '\\'
                self.syntax-error(user "illegal character constant", user text)
        else-if text.length = 4
            if text[1] != '\\'
                self.syntax-error(user "illegal character constant", user text)
            if not self.char-from-escape(copy text[2])->(var self.value)
                self.syntax-error(user "illegal character constant", user text)
        else-if text.length = 6
            if text[1] != '\\'
                self.syntax-error(user "illegal character constant", user text)
            ch := text[2]
            owner Long value
            if ch = 'x'
                if not get-number-value(user text[3:2], copy 16)->(owner value)
                    self.syntax-error(
                            user "illegal character constant", user text)
            else
                if not get-number-value(user text[2:3], copy 8)->(owner value)
                    self.syntax-error(
                            user "illegal character constant", user text)
            self.value := value.int()
        else
            self.syntax-error(user "illegal character constant", user text)
        if text[text.length - 1] != '\''
            self.syntax-error(user "illegal character constant", user text)
        self.set-simple-type(user glob.type-char)
        self.access := Access.VAR
        self.text := text
        
    func inst char-from-escape(copy Char escape)->(
            var Char value, var Bool is-legal)
        user String escapes(user "'\"?\\abfnrtv0")
        user String values(user "\'\"\?\\\a\b\f\n\r\t\v\0")
        for n in escapes.length
            if escape = escapes[n]
                value := values[n]
                is-legal := true
                return
        is-legal := false
    
    func dynamic get-constant-value()->(owner Long value, var Bool has-value)
        value := Long()
        value.set(copy self.value)
        has-value := true


class ConstantBufferVariable(SyntaxTreeVariable)
    user String text
    
    new(user SyntaxTreeBlock parent, user String text)
        base(user parent)
        self.text := text
    
    func dynamic write()
        self.write-constant(user "Byte")

    func inst write-constant(user String type-name)
        self.write-spaces()
        write(user "static ")
        write(user type-name)
        write(user " ")
        self.write-cname()
        write(user "[] = {")
        self.write-data()
        write(user "};\n")
        self.write-spaces()
        write(user "int ")
        self.write-cname()
        write(user "_Max_length = sizeof(")
        self.write-cname()
        write(user ");\n")
        self.write-spaces()
        write(user "int ")
        self.write-cname()
        write(user "_Length[1] = {sizeof(")
        self.write-cname()
        self.write-length-offset()
        write(user "};\n")
    
    func dynamic write-data()
        for n in self.text.length div 2
            write(user "0x")
            write-c(copy self.text[2 * n])
            write-c(copy self.text[(2 * n) + 1])
            write-c(copy ',')
            self.write-new-line(copy n)
        
    func dynamic write-length-offset()
        write(user ")")
        
    func inst write-new-line(copy Int index)
        if (index + 1) mod 20 = 0
            write-c(copy '\n')
            self.write-spaces()
            write-spaces(copy INDENTATION-SPACES)


~~~ Constant buffer expression ~~~
class BufferExpression(TextExpression)
    user ConstantBufferVariable aux-variable
    
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new BufferExpression buffer-expression(user code-node)
        buffer-expression.parse(owner text, user glob.type-buffer)
        expression := buffer-expression
    
    func inst parse(owner String text, user TypeData sequence-type)
        self.text := text
        self.check-and-modify-text()
        self.set-simple-type(user sequence-type)
        self.access := Access.VAR
        self.is-var := true
    
    func dynamic check-and-modify-text()
        if self.text.length < 2
            self.syntax-error(user "too short buffer constant", user self.text)
        if self.text[0] != '`' or self.text[self.text.length - 1] != '`'
            self.syntax-error(
                    user "no '`' around buffer constant", user self.text)
        var Int index-write(copy 0)
        for index-read in 1:self.text.length - 1
            var Char ch(copy self.text[index-read])
            if ch = '\n'
                self.check-indentation()->(var index-read)
            else-if (ch < 'a' or ch > 'f') and (ch < 'A' or ch > 'F') and
                    (ch < '0' or ch > '9')
                var String{2} char-str
                char-str.append(copy ch)
                self.syntax-error(user "illegal buffer data", user char-str)
            else
                self.text[index-write] := ch
                index-write += 1
        self.text.length := index-write
        if self.text.length mod 2 != 0
            self.syntax-error-msg(user "odd buffer data length")
        
    func inst check-indentation()->(var Int index-read)
        var Int expected(copy self.code-node.parent.indentation-spaces +
                (2 * INDENTATION-SPACES))
        var Int indentation(copy 0)
        do
            index-read += 1
            while index-read < self.text.length - 1
            while self.text[index-read] = ' '
            indentation += 1
            while indentation < expected
        if indentation < expected
            self.code-node.syntax-error-indentation(
                    copy indentation, copy expected)
    
    func dynamic analyze()
        self.add-aux-variable(owner ConstantBufferVariable(
                user self.code-node.parent, user self.text))
    
    func inst add-aux-variable(owner ConstantBufferVariable variable)
        variable.access := Access.VAR
        variable.is-create := true
        self.aux-variable := variable
        self.code-node.parent.set-aux-variable(
                owner variable, user self.result-type)
    
    func dynamic write()
        self.aux-variable.write-cname()


class ConstantStringVariable(ConstantBufferVariable)
    func dynamic write()
        self.write-constant(user "char")
    
    func dynamic write-data()
        var Int index
        for n in self.text.length
            write-c(copy '\'')
            if self.text[n] = '\''
                write-c(copy '\\')
            write-c(copy self.text[n])
            if self.text[n] = '\\'
                n += 1
                write-c(copy self.text[n])
            write-c(copy '\'')
            write-c(copy ',')
            self.write-new-line(copy index)
            index += 1
        
    func dynamic write-length-offset()
        write(user ") - 1")

~~~ Constant string expression ~~~
class StringExpression(BufferExpression)
    func inst parse-new(owner String text, user SyntaxTreeCode code-node)->(
            owner Expression expression)
        new StringExpression string-expression(user code-node)
        string-expression.parse(owner text, user glob.type-string)
        expression := string-expression
    
    func dynamic check-and-modify-text()
        self.check-string(user self.text)
        var Int index-write(copy 0)
        for index-read in 1:self.text.length - 1
            if self.text[index-read] = '\n'
                var Bool skip-new-line
                if index-read > 0
                    skip-new-line := self.text[index-read - 1] = '\\'
                self.check-indentation()->(var index-read)
                if skip-new-line
                    index-write -= 1
                else
                    self.text[index-write] := '\\'
                    index-write += 1
                    self.text[index-write] := 'n'
                    index-write += 1
            else
                self.text[index-write] := self.text[index-read]
                index-write += 1
        self.text[index-write] := '\\'
        index-write += 1
        self.text[index-write] := '0'
        index-write += 1
        self.text.length := index-write

    func dynamic analyze()
        self.add-aux-variable(owner ConstantStringVariable(
                user self.code-node.parent, user self.text))


~~~ Constant empty expression `_` ~~~
class EmptyExpression(Expression)
    new(user SyntaxTreeCode code-node)
        base(user code-node)
        self.set-simple-type(user glob.type-empty)
        self.access := Access.STRONG
    
    func dynamic write()
        write(user "NULL")
    
    func dynamic write-dynamic()
        self.write()
    
    func dynamic write-refman()
        self.write()
