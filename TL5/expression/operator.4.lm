~~~ TL5 compiler - Operators ~~~
module tl5-compiler


struct Operator
    owner String name
    owner String c-name
    var Int precedence
    var Int group-index
    var Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner UnaryExpression expression)} unary-expression-factory
    var Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner BinaryExpression expression)} binary-expression-factory

    func new(
            user String name,
            user String c-name,
            copy Int precedence,
            copy Int group-index,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner UnaryExpression expression)} unary-expression-factory,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner BinaryExpression expression)} binary-expression-factory)
        string-new-copy(user name)->(owner self.name)
        string-new-copy(user c-name)->(owner self.c-name)
        self.precedence := precedence
        self.group-index := group-index
        self.unary-expression-factory := unary-expression-factory
        self.binary-expression-factory := binary-expression-factory

    func create-unary-expression(user SyntaxTreeCode code-node)->(
            owner UnaryExpression expression)
        if not self.unary-expression-factory?
            code-node.syntax-error(
                    user "used non-unary operator", user self.name)
        self.unary-expression-factory(user code-node, user self)->(
                owner expression)

    func create-binary-expression(user SyntaxTreeCode code-node)->(
            owner BinaryExpression expression)
        if not self.binary-expression-factory?
            code-node.syntax-error(
                    user "used non-binary operator", user self.name)
        self.binary-expression-factory(user code-node, user self)->(
                owner expression)


func internal-binary-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := BinaryExpression(user code-node, user operator)


~~~ not ~~~
class NotOperatorExpression(UnaryExpression)
    func dynamic analyze()
        base()
        self.test-operand-type(
                user self.right-expression, user glob.type-bool)
        self.set-simple-type(user glob.type-bool)

func not-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner UnaryExpression expression)
    expression := NotOperatorExpression(user code-node, user operator)


~~~ bnot ~~~
class BitwizeNotOperatorExpression(UnaryExpression)
    func dynamic analyze()
        base()
        self.test-operand-type(
                user self.right-expression, user glob.type-int)
        self.check-not-signed(user self.right-expression)
        self.set-uint-type(user self.right-expression)

    func dynamic write()
        write(user "(")
        self.result-type.write-cname()
        write(user ")(")
        base()
        write(user ")")

func bitwize-not-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner UnaryExpression expression)
    expression := BitwizeNotOperatorExpression(user code-node, user operator)


~~~ - (unary) ~~~
class NegationOperatorExpression(UnaryExpression)
    func dynamic analyze()
        base()
        self.test-operand-type(user self.right-expression, user glob.type-int)
        self.set-simple-type(user glob.type-int)
        self.result-type.int-range := IntRange()
        self.right-expression.result-type.int-range.max-value.copy()->(
                owner self.result-type.int-range.min-value)
        self.right-expression.result-type.int-range.min-value.copy()->(
                owner self.result-type.int-range.max-value)
        self.result-type.int-range.min-value.negate()
        self.result-type.int-range.max-value.negate()
        self.result-type.int-range.set-sign-and-bits(user self)

func negation-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner UnaryExpression expression)
    expression := NegationOperatorExpression(user code-node, user operator)


class LimitCheck
    user UnaryExpression expression
    var Bool is-check
    var Bool check-min
    var Bool check-max
    
    new(user UnaryExpression expression)
        self.expression := expression
        self.expression.error-expected := glob.last-char = '!'
        self.expression.error-propagated := self.expression.error-expected
        self.is-check := self.expression.error-expected or glob.last-char = '?'
        if self.is-check
            read-c()
    
    func inst no-checks()->(var Bool no-checks)
        no-checks := not self.check-min and not self.check-max
    
    func inst analyze()
        if self.expression.error-propagated
            self.expression.check-error-propagated(user _)
        else-if self.is-check
            if self.expression.code-node.set-has-error()
                self.expression.syntax-error-msg(
                        user "using \"?\" where error is propagated")

    func inst write-preactions()
        if not self.is-check
            return
        ; if (`min check` || `max check`) RAISE(`integer_overflow`)
        write(user "if (")
        if self.check-min
            self.write-min-check()
        if self.check-max
            if self.check-min
                write(user " || ")
            self.write-max-check()
        write(user ") ")
        self.expression.code-node.write-raise(user "integer_overflow")
        self.expression.code-node.write-spaces()
    
    func dynamic write-min-check()
        ; `exp` < `min`
        self.expression.get-first-operand().write-safe()
        write(user " < ")
        write-long(user self.expression.result-type.int-range.min-value)
    
    func dynamic write-max-check()
        ; `exp` > `max`
        self.expression.get-first-operand().write-safe()
        write(user " > ")
        write-long(user self.expression.result-type.int-range.max-value)


~~~ clamp ~~~
class ClampOperatorExpression(UnaryExpression)
    var LimitCheck check
    
    new(user SyntaxTreeCode code-node, user Operator operator)
        self.check.new(user self)
        base(user code-node, user operator)
    
    func dynamic analyze()
        if not self.result-type?
            self.syntax-error(user "misplaced use of", user "clamp")
        base()
        self.test-operand-type(user self.right-expression, user glob.type-int)
        user IntRange source(user self.right-expression.result-type.int-range)
        user IntRange target(user self.result-type.int-range)
        long-larger(user target.min-value, user source.min-value)->(
                var self.check.check-min)
        long-larger(user source.max-value, user target.max-value)->(
                var self.check.check-max)
        if self.check.no-checks()
            self.syntax-error(user "unnecessary use of", user "clamp")
        self.result-type.type-data := glob.type-int
        self.check.analyze()

    func dynamic write-preactions()
        base()
        self.check.write-preactions()

    func dynamic write()
        if self.check.is-check
            self.right-expression.write()
            return
        ; (`exp` < `min`)? `min`: ((`exp` > `max`)? `max`: `exp`)
        if self.check.check-min
            self.write-clamp(
                    user " < ", user self.result-type.int-range.min-value)
        if self.check.check-max
            if self.check.check-min
                write(user "(")
            self.write-clamp(
                    user " > ", user self.result-type.int-range.max-value)
        self.right-expression.write-safe()
        if self.check.check-min and self.check.check-max
            write(user ")")
    
    func inst write-clamp(user String compare, user Long value)
        write(user "(")
        self.right-expression.write-safe()
        write(user compare)
        write-long(user value)
        write(user ")? ")
        write-long(user value)
        write(user ": ")

func clamp-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner UnaryExpression expression)
    expression := ClampOperatorExpression(user code-node, user operator)

~~~ wraparound ~~~
class WraparoundOperatorExpression(UnaryExpression)
    func dynamic analyze()
        if not self.result-type?
            self.syntax-error(user "misplaced use of", user "wraparound")
        base()
        self.test-operand-type(user self.right-expression, user glob.type-int)
        self.set-uint-type(user self)
        user IntRange range(user self.right-expression.result-type.int-range)
        if not range.is-signed and
                range.bits <= self.result-type.int-range.bits
            self.syntax-error(user "unnecessary use of", user "wraparound")
    
    func dynamic write()
        self.right-expression.write()

func wraparound-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner UnaryExpression expression)
    expression := WraparoundOperatorExpression(user code-node, user operator)


class BitwizeOperatorExpression(BinaryExpression)
    func dynamic analyze()
        base()
        self.test-operands-type(user glob.type-int)
        self.check-not-signed(user self.left-expression)
        self.check-not-signed(user self.right-expression)
        self.set-uint-type(user self.get-result-operand())
    
    func dynamic get-result-operand()->(user Expression operand)
        if self.right-expression.result-type.int-range.bits >
                self.left-expression.result-type.int-range.bits
            operand := self.right-expression
        else
            operand := self.left-expression
        
func bitwize-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := BitwizeOperatorExpression(user code-node, user operator)


class ShiftOperatorExpression(BitwizeOperatorExpression)
    func dynamic get-result-operand()->(user Expression operand)
        operand := self.left-expression
        
func shift-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := ShiftOperatorExpression(user code-node, user operator)
        
        
class ArithmeticLimitCheck(LimitCheck)
    func dynamic write-min-check()
        self.write-check(user "MIN_", user ")")

    func dynamic write-max-check()
        if self.expression.result-type.int-range.is-signed
            self.write-check(user "MAX_", user ", INT64_MAX)")
        else
            self.write-check(user "MAX_", user ", UINT64_MAX)")
        
    func inst write-check(user String kind, user String end)
        write(user "CHECK_")
        write(user kind)
        var Char operator(copy self.expression.operator.name[0])
        if operator = '+'
            write(user "ADD(")
        else-if operator = '-'
            write(user "SUB(")
        else-if operator = '*'
            write(user "MUL(")
        self.expression.get-first-operand().write-safe()
        write(user ", ")
        self.expression.right-expression.write-safe()
        write(user end)


class ArithmeticOperatorExpression(BinaryExpression)
    var ArithmeticLimitCheck check
    
    new(user SyntaxTreeCode code-node, user Operator operator)
        self.check.new(user self)
        base(user code-node, user operator)
    
    func dynamic analyze()
        base()
        self.test-operands-type(user glob.type-int)
        self.set-simple-type(user glob.type-int)
        new IntRange int-range
        self.set-range(
                user int-range,
                user self.left-expression.result-type.int-range.min-value,
                user self.left-expression.result-type.int-range.max-value,
                user self.right-expression.result-type.int-range.min-value,
                user self.right-expression.result-type.int-range.max-value)
        if self.check.is-check
            var Long limit
            if int-range.min-value.sign() < 0
                limit.parse(user "-9223372036854775808")
                if long-larger(user limit, user int-range.min-value)->(
                        var self.check.check-min)
                    limit.copy()->(owner int-range.min-value)
                limit.parse(user "9223372036854775807")
                if long-larger(user int-range.max-value, user limit)->(
                        var self.check.check-max)
                    limit.copy()->(owner int-range.max-value)
            else
                limit.parse(user "18446744073709551615")
                if long-larger(user int-range.max-value, user limit)->(
                        var self.check.check-max)
                    limit.copy()->(owner int-range.max-value)
            if self.check.no-checks()
                self.check-no-error()
                self.syntax-error-msg(
                        user "using \"?\" where there is no error")
        int-range.set-sign-and-bits(user self)
        self.result-type.int-range := int-range
        self.check.analyze()
    
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        raise
    
    func dynamic write-preactions()
        base()
        self.check.write-preactions()
    
    func dynamic write()
        if (self.result-type.int-range.bits >
                self.left-expression.result-type.int-range.bits and
                self.result-type.int-range.bits >
                self.right-expression.result-type.int-range.bits)
            self.write-int-cast-operand(
                    user self.left-expression,
                    copy self.left-expression.result-type.int-range.is-signed,
                    copy self.result-type.int-range.bits)
        else
            self.write-operand(user self.left-expression)
        write(user " ")
        self.write-end()


~~~ + ~~~
class AdditionOperatorExpression(ArithmeticOperatorExpression)
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        long-add(user left-min, user right-min)->(owner int-range.min-value)
        long-add(user left-max, user right-max)->(owner int-range.max-value)

func addition-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := AdditionOperatorExpression(user code-node, user operator)


~~~ - ~~~
class SubtractionOperatorExpression(ArithmeticOperatorExpression)
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        long-sub(user left-min, user right-max)->(owner int-range.min-value)
        long-sub(user left-max, user right-min)->(owner int-range.max-value)

func subtraction-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := SubtractionOperatorExpression(user code-node, user operator)


~~~ * ~~~
class MultiplicationOperatorExpression(ArithmeticOperatorExpression)
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        long-mul(user left-min, user right-min)->(owner int-range.min-value)
        int-range.min-value.copy()->(owner int-range.max-value)
        self.limit-candidate(user int-range, user left-min, user right-max)
        self.limit-candidate(user int-range, user left-max, user right-min)
        self.limit-candidate(user int-range, user left-max, user right-max)
    
    func inst limit-candidate(
            user IntRange int-range, user Long a, user Long b)
        owner Long mul
        long-mul(user a, user b)->(owner mul)
        if long-larger(user int-range.min-value, user mul)
            int-range.min-value := mul
        else-if long-larger(user mul, user int-range.max-value)
            int-range.max-value := mul

func multiplication-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression :=
            MultiplicationOperatorExpression(user code-node, user operator)


~~~ div ~~~
class DivisionOperatorExpression(ArithmeticOperatorExpression)
    var Bool zero-check
    
    func inst check-division(user Long right-min, user Long right-max)
        self.check-not-signed(user self.left-expression)
        self.check-not-signed(user self.right-expression)
        if right-max.sign() = 0
            self.syntax-error-msg(user "dividing by zero")
        self.zero-check := right-min.sign() = 0
    
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        self.check-division(user right-min, user right-max)
        if right-min.sign() = 0
            left-max.copy()->(owner int-range.max-value)
        else
            long-div(user left-max, user right-min)->(owner int-range.max-value)
        long-div(user left-min, user right-max)->(owner int-range.min-value)
    
    func dynamic analyze()
        self.allow-right-error := true
        base()
        if self.zero-check
            self.right-expression.check-error-propagated(
                    user "ignoring zero division check")
        else
            self.right-expression.check-no-error()

    func dynamic write-preactions()
        base()
        if not self.zero-check
            return
        ; if (`right` == 0) RAISE(`line`, `cleanup`, zero_division)
        write(user "if (")
        self.right-expression.write()
        write(user " == 0) ")
        self.code-node.write-raise(user "zero_division")
        self.code-node.write-spaces()

func division-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := DivisionOperatorExpression(user code-node, user operator)


~~~ mod ~~~
class ModuloOperatorExpression(DivisionOperatorExpression)
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        self.check-division(user right-min, user right-max)
        if long-equal(user left-min, user left-max) and
                long-equal(user right-min, user right-max)
            long-mod(user left-min, user right-min)->(owner int-range.min-value)
            int-range.min-value.copy()->(owner int-range.max-value)
        else
            int-range.min-value := Long()
            if long-larger(user right-max, user left-max)
                left-max.copy()->(owner int-range.max-value)
            else
                var Long one
                one.set(copy 1)
                long-sub(user right-max, user one)->(owner int-range.max-value)

func modulo-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := ModuloOperatorExpression(user code-node, user operator)


~~~ clamp+, clamp-, clamp* ~~~
class ClampedArithmeticOperatorExpression(ArithmeticOperatorExpression)
    func dynamic set-range(
            user IntRange int-range,
            user Long left-min,
            user Long left-max,
            user Long right-min,
            user Long right-max)
        left-min.copy()->(owner int-range.min-value)
        left-max.copy()->(owner int-range.max-value)
    
    func dynamic write()
        write(user self.operator.c-name)
        write-c(copy '_')
        self.write-sign-char(user self.left-expression)
        self.write-sign-char(user self.right-expression)
        write-c(copy '(')
        self.left-expression.write-safe()
        write(user ", ")
        self.right-expression.write-safe()
        write(user ", ")
        write-long(user self.left-expression.result-type.int-range.min-value)
        write(user ", ")
        write-long(user self.left-expression.result-type.int-range.max-value)
        write-c(copy ')')
    
    func inst write-sign-char(user Expression expression)
        if expression.result-type.int-range.is-signed
            write-c(copy 'S')
        else
            write-c(copy 'U')

func clamped-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := ClampedArithmeticOperatorExpression(
            user code-node, user operator)


~~~ wraparound+, wraparound-, wraparound* ~~~
class WraparoundArithmeticOperatorExpression(BinaryExpression)
    owner ArithmeticOperatorExpression arithmetic
    
    new(
            owner ArithmeticOperatorExpression arithmetic,
            user SyntaxTreeCode code-node,
            user Operator operator)
        base(user code-node, user operator)
        self.arithmetic := arithmetic
    
    func dynamic analyze()
        base()
        self.test-operands-type(user glob.type-int)
        self.check-not-signed(user self.left-expression)
        self.set-uint-type(user self.left-expression)
        var IntRange arithmetic-range
        self.arithmetic.set-range(
                user arithmetic-range,
                user self.left-expression.result-type.int-range.min-value,
                user self.left-expression.result-type.int-range.max-value,
                user self.right-expression.result-type.int-range.min-value,
                user self.right-expression.result-type.int-range.max-value)
        if arithmetic-range.set-sign-and-bits(user _) and
                not arithmetic-range.is-signed and
                arithmetic-range.bits <= self.result-type.int-range.bits
            self.syntax-error(
                    user "unnecessary wraparound with",
                    user self.operator.c-name)
    
    func dynamic write()
        write-c(copy '(')
        self.result-type.write-cname()
        write(user ")(")
        base()
        write-c(copy ')')

    func dynamic write-safe()
        self.write()
        

func wraparound-addition-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := WraparoundArithmeticOperatorExpression(
            owner AdditionOperatorExpression(user _, user _),
            user code-node,
            user operator)

func wraparound-subtraction-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := WraparoundArithmeticOperatorExpression(
            owner SubtractionOperatorExpression(user _, user _),
            user code-node,
            user operator)

func wraparound-multiplication-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := WraparoundArithmeticOperatorExpression(
            owner MultiplicationOperatorExpression(user _, user _),
            user code-node,
            user operator)


~~~ = != ~~~
class EqualizerOperatorExpression(BinaryExpression)
    ~~~ expand-and: (a > b) > c --> (a > b) && (b > c) ~~~
    var Bool expand-and
    
    func dynamic analyze()
        base()
        if self.binary-left-expression?
            self.binary-left-expression.is-relational()->(var self.expand-and)
        self.test-operand(user self.right-expression)
        if not self.expand-and
            self.test-operand(user self.left-expression)
        self.set-simple-type(user glob.type-bool)

    func dynamic is-relational()->(var Bool is-relational)
        is-relational := true
        
    func dynamic test-operand(user Expression operand)
        self.test-is-reference(copy false, user operand)

    func dynamic write-end()
        if self.expand-and
            write(user "&& (")
            self.write-operand(
                    user self.binary-left-expression.right-expression)
            write(user " ")
        base()
        if self.expand-and
            write(user ")")

func equalizer-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := EqualizerOperatorExpression(user code-node, user operator)


~~~ > < >= <= ~~~
class RelationalOperatorExpression(EqualizerOperatorExpression)
    var IntRange common-range
    
    func dynamic test-operand(user Expression operand)
        self.test-operand-type(user operand, user glob.type-int)

    func dynamic analyze()
        base()
        user Expression left-expression(user self.left-expression)
        if self.expand-and
            left-expression := self.binary-left-expression.right-expression
        user IntRange left(user left-expression.result-type.int-range)
        user IntRange right(user self.right-expression.result-type.int-range)
        if long-larger(user left.max-value, user right.max-value)
            left.max-value.copy()->(owner self.common-range.max-value)
        else
            right.max-value.copy()->(owner self.common-range.max-value)
        if long-larger(user left.min-value, user right.min-value)
            right.min-value.copy()->(owner self.common-range.min-value)
        else
            left.min-value.copy()->(owner self.common-range.min-value)
        self.common-range.set-sign-and-bits(user self)

    func dynamic write-operand(user Expression operand)
        if self.common-range.is-signed
            self.write-int-cast-operand(
                    user operand, copy true, copy self.common-range.bits)
        else
            base(user operand)

func relational-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := RelationalOperatorExpression(user code-node, user operator)


~~~ is is-not ~~~
class IdentityOperatorExpression(BinaryExpression)
    func dynamic analyze()
        base()
        self.test-is-reference(copy true, user self.right-expression)
        self.test-is-reference(copy true, user self.left-expression)
        self.set-simple-type(user glob.type-bool)
        self.safe-operand-write := false

    func dynamic write()
        write(user "(void*)")
        base()

func identity-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := IdentityOperatorExpression(user code-node, user operator)


~~~ or and ~~~
class LogicalOperatorExpression(BinaryExpression)
    func dynamic analyze()
        base()
        self.test-operands-type(user glob.type-bool)
        self.set-simple-type(user glob.type-bool)


func logical-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := LogicalOperatorExpression(user code-node, user operator)


~~~ {*}= ~~~
class BaseAssignOperatorExpression(BinaryExpression)
    func dynamic analyze-right-and-all()
        base()
        if not self.left-expression.assignable
            self.syntax-error-msg(
                    user "assigning into non assignable expression")
        self.analyze-assignment()
        self.safe-operand-write := false
        self.set-simple-type(user glob.type-void-expression)

    func dynamic analyze-assignment()
        raise


~~~ := ~~~
class AssignOperatorExpression(BaseAssignOperatorExpression)
    var AssignExpression assign
    
    func dynamic analyze()
        self.analyze-operand()->(owner self.left-expression)
        self.left-expression.result-type.set-expected-type()->(
                owner self.right-expression.result-type)
        self.analyze-right-and-all()

    new(user SyntaxTreeCode code-node, user Operator operator)
        base(user code-node, user operator)
        self.allow-right-error := true

    func dynamic analyze-assignment()
        self.assign.new(
                user self.code-node,
                user self.left-expression,
                user self.right-expression,
                copy false)
        self.assign.analyze-value()->(owner self.right-expression)

    func dynamic check-memory(user ReferenceMemoryList refs)
        self.assign.check-memory(user refs)

    func dynamic write-preactions()
        self.assign.write-preactions()

    func dynamic write-end()
        if access-is-owner(copy self.left-expression.access)
            self.write-end-expression(user self.assign.value)
            self.assign.write-owner-null()
        else
            base()

func assign-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := AssignOperatorExpression(user code-node, user operator)


~~~ clamp+= clamp-= clamp*= ~~~
class ClampedAssignOperatorExpression(BaseAssignOperatorExpression)
    func dynamic analyze-assignment()
        self.test-operands-type(user glob.type-int)
    
    func dynamic write()
        self.write-operand(user self.left-expression)
        write(user " ")
        var ClampedArithmeticOperatorExpression clamped-expression(
                user self.code-node, user self.operator)
        clamped-expression.left-expression := self.left-expression
        clamped-expression.right-expression := self.right-expression
        self.write-operand(user clamped-expression)
        self.left-expression := clamped-expression.left-expression
        self.right-expression := clamped-expression.right-expression
        if self.is-statement
            write(user ";\n")

func clamped-assign-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression :=
            ClampedAssignOperatorExpression(user code-node, user operator)


~~~ wraparound+= wraparound-= wraparound*= ~~~
class WraparoundAssignOperatorExpression(BaseAssignOperatorExpression)
    func dynamic analyze-assignment()
        self.test-operands-type(user glob.type-int)
        var UnaryExpression value(user _, user _)
        value.set-uint-type(user self.left-expression)
        value.result-type.check-assign-to(
                copy Access.VAR,
                user self.left-expression.result-type,
                copy Access.VAR,
                user self)

func wraparound-assign-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression :=
            WraparoundAssignOperatorExpression(user code-node, user operator)

~~~ :=: ~~~
class SwapOperatorExpression(BinaryExpression)
    var AssignExpression assign-left-to-aux
    var AssignExpression assign-right-to-left
    var AssignExpression assign-aux-to-right
    owner Expression aux-variable

    func dynamic analyze()
        base()
        if not self.left-expression.assignable or
                not self.right-expression.assignable
            self.syntax-error-msg(user "swapping non assignable expression")
        self.code-node.add-aux-variable(
                copy self.left-expression.access,
                copy false,
                user self.left-expression.result-type)->(
                owner self.aux-variable)
        self.assign-left-to-aux.new(
                user self.code-node,
                user self.aux-variable,
                user self.left-expression,
                copy false)
        self.assign-right-to-left.new(
                user self.code-node,
                user self.left-expression,
                user self.right-expression,
                copy false)
        self.assign-aux-to-right.new(
                user self.code-node,
                user self.right-expression,
                user self.aux-variable,
                copy false)
        self.left-expression.result-type.check-equal(
                user self.right-expression.result-type, user self.code-node)
        if self.left-expression.access != self.right-expression.access
            self.syntax-error2(
                    user "cannot swap access",
                    user glob.access-names[self.left-expression.access],
                    user "with other access",
                    user glob.access-names[self.right-expression.access])
        if self.left-expression.result-type.reference-path.equals(
                user self.right-expression.result-type.reference-path)
            self.syntax-error-msg(user "swapping reference with itself")
        self.set-simple-type(user glob.type-void-expression)


    func dynamic check-memory(user ReferenceMemoryList refs)
        if access-is-temp(copy self.left-expression.access)
            refs.check-writing-owner-memory(user self.left-expression)
            refs.check-writing-owner-memory(user self.right-expression)
        else
            self.left-expression.check-memory(user refs)
            self.right-expression.check-memory(user refs)

    func dynamic write()
        self.assign-left-to-aux.write-assign()
        self.code-node.write-spaces()
        self.assign-right-to-left.write-assign()
        self.code-node.write-spaces()
        self.assign-aux-to-right.write-assign()
        if not self.right-expression.result-type.type-data.is-primitive
            self.code-node.write-spaces()
            self.aux-variable.write-assign-null()

func swap-operator-factory(
        user SyntaxTreeCode code-node, user Operator operator)->(
        owner BinaryExpression expression)
    expression := SwapOperatorExpression(user code-node, user operator)
