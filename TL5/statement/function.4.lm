~~~ TL5 compiler - Syntax tree function ~~~
module tl5-compiler

~~~ Function node in the syntax tree ~~~
class SyntaxTreeFunction(SyntaxTreeBlock)
  var List{TypeCount} aux-variables
  var NameMap{SyntaxTreeVariable} function-variables
  var FunctionArguments arguments
  owner String name
  owner String type-name
  owner String module-name
  owner DeleteGroup delete-group-owner
  owner DeleteGroupBuilder delete-group-builder-owner
  user TypeData parent-type
  user ModuleMembers my-module
  user SyntaxTreeFunction implemented-function
  user SyntaxTreeMockFunction mocker-function
  user SyntaxTreeFunction dynamic-base-method
  user DeleteGroup delete-group
  user DeleteGroupBuilder delete-group-builder
  var Int dynamic-base-count
  var Int block-count
  var Bool is-dynamic
  var Bool has-user-assign
  var Bool header-only
  var Bool external-body
  
  func inst parse-new(user TypeData parent-type)->(
      owner SyntaxTreeFunction new-node)
    new-node := SyntaxTreeFunction()
    new-node.parse(user parent-type)
  
  func inst parse(user TypeData parent-type)
    self.parent-type := parent-type
    if self.parent-type?
      self.indentation-spaces := INDENTATION-SPACES
      if self.parent-type.is-dynamic
        user String meth-type
        read-until(user " ", copy false)->(user meth-type)
        if meth-type.equal(user "dynamic")
          self.is-dynamic := true
        else-if not meth-type.equal(user "inst")
          self.syntax-error(
              user "expected \"dynamic\" or \"inst\" method type, got",
              user meth-type)
    else
      self.my-module := glob.current-module
    self.parse-header(copy false)
    self.parse-body()
  
  new()
    self.set-location()
    self.arguments.new()
    self.delete-group-builder-owner := DeleteGroupBuilder(user self)
    self.delete-group-builder := self.delete-group-builder-owner
  
  func inst parse-body()
    if self.header-only
      return
    self.indentation-spaces += INDENTATION-SPACES
    self.parse-block()
    self.indentation-spaces := INDENTATION-SPACES
  
  func inst parse-header(copy Bool is-native)
    read-new(user ".(")->(owner self.name)
    do
      while glob.last-char = '.'
      var Char ch(copy self.name[0])
      var Bool is-module-name(copy ch >= 'a' and ch <= 'z')
      if self.parent-type? or self.type-name? or
          (is-module-name and self.module-name?)
        self.syntax-error(user "misplaced item", user self.name)
      self.external-body := true
      if is-module-name
        self.module-name := self.name
      else
        self.type-name := self.name
      read-new(user ".(")->(owner self.name)
    if glob.last-char != '('
      self.syntax-error-c(user "expected \"(\" after function name, got")
    if not self.module-name? and not self.type-name?
      self.register-name()
    self.parse-arguments(copy is-native)
  
  func inst add-self-parameter()
    if self.parent-type.constructor is self
      self.arguments.add-self-parameter(
          user self.parent-type, copy Access.VAR)
    else
      self.arguments.add-self-parameter(
          user self.parent-type, copy Access.USER)
  
  func inst parse-arguments(copy Bool is-native)
    var DeclarationArgumentFactory argument-factory
    var SyntaxTreeCode dummy-node(user self)
    self.arguments.parse(
        user argument-factory,
        copy is-native,
        user dummy-node,
        copy self.indentation-spaces)
    if self.parent-type?
      self.add-self-parameter()
    if glob.last-char = ' ' and self.parent-type?
      read-c()
      self.header-only := glob.last-char = '_'
      if self.header-only
        read-c()
      else
        glob.last-char := ' '
  
  func dynamic register-name()
    if not is-legal-name(user self.name, copy NameGroup.DEFAULT)
      self.syntax-error(user "illegal function name", user self.name)
    if self.parent-type?
      user SyntaxTreeFunction meth
      self.parent-type.find-meth(user self.name)->(user meth)
      if meth?
        self.syntax-error(user "redefinition of method", user self.name)
      user SyntaxTreeVariable field
      self.parent-type.find-field(user self.name)->(user field)
      if field?
        self.syntax-error(
            user "method name overrides field", user self.name)
    else
      if glob.module-map.find(user self.name)?
        self.syntax-error(
            user "function name overrides module", user self.name)
      if glob.find-function(user self.name)?
        self.syntax-error(user "redefinition of function", user self.name)
      if glob.find-variable(user self.name)?
        self.syntax-error(
            user "function name overrides global variable", user self.name)
      if self.my-module?
        self.my-module.function-map.add(user self.name, user self)
  
  func dynamic get-function()->(user SyntaxTreeFunction function)
    function := self
  
  func dynamic get-parent-type()->(user TypeData parent-type)
    parent-type := self.parent-type
  
  func dynamic find-variable(user String name, user String module-name)->(
      user SyntaxTreeVariable variable)
    if base(user name, user module-name)->(user variable)?
      return
    if not module-name?
      self.arguments.find-variable(user name, user _)->(user variable)
    
  func inst new-aux-variable-index(user TypeInstance type-instance)->(
      var Int index)
    var ListIterator{TypeCount} iter(user self.aux-variables)
    do
      while iter.has()
      while iter.get().type-data is-not type-instance.type-data
      iter.next()
    if iter.has()
      iter.get().count += 1
      index := iter.get().count
    else
      new TypeCount type-count
      type-count.type-data := type-instance.type-data
      self.aux-variables.add(owner type-count)
      index := 0
  
  func dynamic link-types()
    if self.my-module?
      glob.current-module := self.my-module
    if self.type-name?
      self.find-type(user self.type-name, user self.module-name)->(
          user self.parent-type)
      self.parent-type.find-meth(user self.name)->(
          user self.implemented-function)
      if not self.implemented-function?
        self.syntax-error2(
            user "unknown method",
            user self.name,
            user "of type",
            user self.type-name)
      self.check-already-implemented()
      if self.parent-type.constructor is self.implemented-function
        self.parent-type.constructor := self
      self.add-self-parameter()
    self.arguments.link-types()
    base()
    if self.my-module?
      glob.current-module := _
  
  func dynamic check-already-implemented()
    if not self.implemented-function.header-only or
        self.implemented-function.external-body
      self.syntax-error2(
          user "type",
          user self.type-name,
          user "already implemented method",
          user self.name)
    self.implemented-function.external-body := true
  
  func dynamic analyze()
    if self.my-module?
      glob.current-module := self.my-module
    if self.implemented-function?
      self.arguments.check-same-as(
          user self.implemented-function.arguments, user _, copy 0, copy true)
      self.my-module := self.implemented-function.my-module
    self.arguments.analyze()
    if self.header-only
      return
    for parameter in self.arguments.parameters.iter()
      if access-is-owner(copy parameter.access)
        self.delete-group-builder.add-deleting(
            user parameter.get-type-instance())
    base()
    for variable in self.variables.iter()
      if access-is-var(copy variable.access)
        self.delete-group-builder.add-deleting(user variable.type-instance)
    if self.my-module?
      glob.current-module := _
  
  func inst compare(user SyntaxTreeFunction other)
    if self.is-dynamic != other.is-dynamic
      self.syntax-error(user "illegal dynamic in function", user self.name)
    
  func inst collect-delete-group()->(user DeleteGroupBuilder recursive)
    if self.delete-group?
      return
    self.delete-group-builder.build()->(user recursive)
    
  func dynamic check-memory(user ReferenceMemoryList global-refs)
    if self.header-only
      return
    var ReferenceMemoryList refs
    self.arguments.check-memory(user refs)
    self.check-children-memory(user self.variables, user refs)
    self.check-children-memory(user self.code-nodes, user refs)
    for parameter in self.arguments.parameters.iter()
      if access-is-owner(copy parameter.access)
        refs.mark-illegal-type(user parameter.get-type-instance())
    for variable in self.variables.iter()
      if access-is-var(copy variable.access)
        refs.mark-illegal-type(user variable.type-instance)
    for output in self.arguments.outputs.iter()
      refs.check-output(user output)
  
  ~~~ write function body ~~~
  func dynamic write()
    ; #define LUMI_FILE_NAME "`file-name`"
    ; #define LUMI_FUNC_NAME "`parent-type`.`function-name`"
    ; Returncode `Type`_`name`(`args`) {
    ;   `block...`
    ; }
    ; #undef LUMI_FILE_NAME
    ; #undef LUMI_FUNC_NAME
    if self.header-only
      return
    self.write-pre-func()
    self.write-header()
    self.write-block()
    self.write-post-func()
  
  func inst write-header()
    write(user "\nReturncode ")
    self.write-cname()
    self.arguments.write()
  
  func inst write-block()
    ; `previous-text` {
    ;   Returncode LUMI_err = OK;
    ;   unsigned LUMI_loop_depth = 1;
    ;   `block...`
    ;   return LUMI_err;
    ; }
    self.write-block-start()
    write-spaces(copy INDENTATION-SPACES)
    write(user "Returncode LUMI_err = OK;\n")
    write-spaces(copy INDENTATION-SPACES)
    write(user "unsigned LUMI_loop_depth = 1;\n")
    self.write-block-setup()
    self.write-setup()
    self.write-block-body()
    self.write-block-cleanup()
    self.write-param-cleanup()
    write-spaces(copy INDENTATION-SPACES)
    write(user "return LUMI_err;\n")
    self.write-block-end()
  
  func inst write-setup()
    if self.has-user-assign
      write-spaces(copy INDENTATION-SPACES)
      write(user "Ref_Manager* aux_Ref_Manager = NULL;\n")
    for parameter in self.arguments.parameters.iter()
      if access-is-user(copy parameter.access)
        write-spaces(copy INDENTATION-SPACES)
        write(user "LUMI_inc_ref(")
        write-cname(user parameter.get-variable().name)
        write(user "_Refman);\n")
  
  func inst write-param-cleanup()
    for parameter in self.arguments.parameters.iter-back()
      if access-is-user(copy parameter.access) or
          access-is-owner(copy parameter.access)
        self.write-variable-cleanup(user parameter.get-variable(), user _)
  
  func inst write-cname()
    if self.parent-type?
      self.parent-type.write-cname()
      write(user "_")
    else-if self.my-module?
      self.my-module.write-prefix()
    write-cname(user self.name)
  
  func inst write-pointer()
    self.arguments.write-pointer(user self.name)
  
  func dynamic write-declaration()
    ; Returncode `Type`_`name`(`args`);
    if self.external-body and not self.header-only
      return
    self.write-header()
    write(user ";\n")
  
  func inst write-pre-func()
    write(user "\n#define LUMI_FILE_NAME \"")
    write(user self.input-file-name)
    write(user "\"\n#define LUMI_FUNC_NAME \"")
    if self.parent-type?
      write(user self.parent-type.name)
      write(user ".")
    write(user self.name)
    write(user "\"")
    
    ; write(user "\n/* calling[")
    ; for function in self.calling-functions.iter()
    ;   write(user "|")
    ;   if function.parent-type?
    ;     write(user function.parent-type.name)
    ;     write(user ".")
    ;   write(user function.name)
    ; write(user "|] */")
    ; 
    ; write(user "\n/* deleting[")
    ; for type-data in self.deleting-types.iter()
    ;   write(user "|")
    ;   write(user type-data.name)
    ; write(user "|] */")
      
  
  func inst write-post-func()
    write(user "#undef LUMI_FILE_NAME\n")
    write(user "#undef LUMI_FUNC_NAME\n")


~~~ Count occurrences of a specific type ~~~
struct TypeCount
  user TypeData type-data
  var Int count


~~~ Constructor declaration in the syntax tree ~~~
class SyntaxTreeConstructor(SyntaxTreeFunction)
  func inst parse-new(user TypeData parent-type)->(
      owner SyntaxTreeConstructor new-node)
    new-node := SyntaxTreeConstructor()
    new-node.parse(user parent-type)
  
  func inst parse(user TypeData parent-type)
    self.parent-type := parent-type
    self.indentation-spaces := INDENTATION-SPACES
    self.parent-type.constructor := self
    string-new-copy(user "new")->(owner self.name)
    self.parse-arguments(copy false)
    self.parse-body()
  
  func dynamic analyze()
    if self.arguments.outputs.first?
      self.syntax-error-msg(user "constructor with outputs")
    base()


~~~ Function declaration argument ~~~
class DeclarationArgument(Argument)
  owner SyntaxTreeVariable variable
  
  func dynamic copy-new()->(owner DeclarationArgument new-argument)
    new-argument := DeclarationArgument()
    new-argument.input-file-name := self.input-file-name
    new-argument.line-number := self.line-number
    new-argument.access := self.access
    new-argument.is-output := self.is-output
    new-argument.is-native := self.is-native
    new-argument.variable := SyntaxTreeVariable(user _)
    new-argument.variable.access := self.variable.access
    new-argument.variable.parent-type := self.variable.parent-type
    self.variable.type-instance.copy-new()->(
        owner new-argument.variable.type-instance)
    string-new-copy(user self.variable.name)->(owner new-argument.variable.name)
  
  func dynamic parse-value(
      user SyntaxTreeCode code-node, copy Int indentation-spaces)
    self.variable := SyntaxTreeVariable(user _)
    self.variable.access := self.access
    self.variable.is-output := self.is-output
    if code-node?
      self.variable.parent := code-node.parent
    self.variable.type-instance := TypeInstance()
    self.variable.type-instance.parse(
        user " ", user self, user _, copy indentation-spaces)
    if glob.last-char != ' '
      self.syntax-error-c(user "expected space after type, got")
    read-new(user ",)")->(owner self.variable.name)
    self.variable.check-name()
  
  func dynamic get-parent-type()->(user TypeData parent-type)
    self.variable.get-parent-type()->(user parent-type)
  
  func dynamic link-types()
    self.variable.type-instance.link-types(user self)
  
  func dynamic analyze()
    if self.variable.type-instance.type-data.is-primitive
      if self.is-output and self.access != Access.VAR
        self.syntax-error2(
            user "output",
            user self.variable.name,
            user "access should be \"var\" for primitive types, got",
            user glob.access-names[self.access])
      else-if not self.is-output and self.access != Access.COPY
        self.syntax-error2(
            user "parameter",
            user self.variable.name,
            user "access should be \"copy\" for primitive types, got",
            user glob.access-names[self.access])
    else-if self.access = Access.COPY
      self.syntax-error2(
          user "argument",
          user self.variable.name,
          user "access should not be \"copy\" for non-primitive type",
          user self.variable.type-instance.type-data.name)
    else-if self.is-output and access-is-only-var(copy self.access)
      self.syntax-error2(
          user "output",
          user self.variable.name,
          user "access should not be \"var\" for non-primitive type",
          user self.variable.type-instance.type-data.name)
    else-if self.access = Access.RESET
      self.syntax-error(
          user "access should not be \"reset\" for argument",
          user self.variable.name)
    self.variable.type-instance.analyze-lengths(user self, copy false)
    if self.is-native
      if access-is-owner(copy self.access)
        self.syntax-error-msg(user "owner argument to native function")
      if self.is-output and access-is-user(copy self.access)
        self.syntax-error-msg(user "user output to native function")
  
  func dynamic get-type-instance()->(user TypeInstance type-instance)
    type-instance := self.variable.type-instance
  
  func dynamic get-variable()->(user SyntaxTreeVariable variable)
    variable := self.variable
  
  func dynamic check-memory(user ReferenceMemoryList refs)
    if not self.is-output
      refs.add(user self.variable)
  
  func dynamic write()
    var Bool is-string-var-ref
    if self.variable.type-instance.type-data is glob.type-func
      ; Returncode (*[*]`name`)(`arguments`)
      write(user "Returncode (*")
      if self.is-output
        write(user "*")
      write-cname(user self.variable.name)
      write(user ")")
      self.variable.type-instance.arguments.write()
      return
    
    if self.variable.type-instance.type-data is glob.type-array
      ; `data-type`*[*] `name`, int[*] `name`_Length
      user TypeInstance data-type
      var Int depth
      self.variable.type-instance.get-array-data-type-depth()->(
          user data-type, var depth)
      if data-type.type-data is glob.type-string
        write(user "char")
      else
        data-type.type-data.write-cname()
      self.write-name-param(user "*", user _)
      self.write-name-param(user ", int", user "_Length")
      for n in depth
        ; , int[*] `name`_Value_length
        self.write-name-param(user ", int", user _)
        write-array-value-length(copy n)
      if data-type.type-data is glob.type-string
        ; , int*[*] `name`_String_length
        self.write-name-param(user ", int*", user "_String_length")
    
    else-if self.variable.type-instance.type-data is glob.type-string
      ; char*[*] `name`, int[*] `name`_Max_length, int[*] `name`_Length
      self.write-name-param(user "char*", user _)
      self.write-name-param(user ", int", user "_Max_length")
      write(user ", int")
      if not self.is-output and access-is-only-var(copy self.variable.access)
        write(user "*")
        is-string-var-ref := true
      self.write-name-param(user _, user "_Length")
    
    else
      ; `type`[*][*] `name`
      self.variable.type-instance.type-data.write-cname()
      if not self.variable.type-instance.type-data.is-primitive
        write(user "*")
      self.write-name-param(user _, user _)
    
    if not self.variable.type-instance.type-data.is-primitive and
        not self.is-native
      ; , Ref_Manager*[*] `name`_Refman
      write(user ", Ref_Manager*")
      if is-string-var-ref
        write(user "*")
      self.write-name-param(user _, user "_Refman")
    
    if self.variable.type-instance.type-data.is-dynamic and not self.is-native
      ; , `type`_Dynamic*[*] `name`_Dynamic
      write(user ", ")
      self.variable.type-instance.type-data.write-cname()
      self.write-name-param(user "_Dynamic*", user "_Dynamic")
  
  func inst write-name-param(user String prefix, user String suffix)
    ; `prefix`[*] `name``suffix`
    if prefix?
      write(user prefix)
    if self.is-output
      write(user "*")
    write(user " ")
    write-cname(user self.variable.name)
    if suffix?
      write(user suffix)


class DeclarationArgumentFactory(ArgumentFactory)
  func dynamic new-argument()->(owner Argument new-argument)
    new-argument := DeclarationArgument()
