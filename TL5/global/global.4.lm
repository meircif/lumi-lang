~~~ TL5 compiler - Global compiler data ~~~
module tl5-compiler

~~~ Access values ~~~
enum Access
    UNDEFINED
    VAR
    COPY
    OWNER
    USER
    TEMP
    STRONG
    WEAK
    S-VAR
    SHARED

func access-is-owner(copy Int access)->(var Bool is-owner)
    is-owner := access = Access.OWNER or access = Access.STRONG or
            access = Access.SHARED

func access-has-refman(copy Int access)->(var Bool is-weak)
    is-weak := access = Access.STRONG or access = Access.WEAK or
            access = Access.S-VAR

func access-is-user(copy Int access)->(var Bool is-user)
    is-user := access = Access.USER or access = Access.WEAK

func access-is-only-var(copy Int access)->(var Bool is-only-var)
    is-only-var := access = Access.VAR or access = Access.S-VAR

func access-is-var(copy Int access)->(var Bool is-var)
    is-var := access-is-owner(copy access) or access-is-only-var(copy access)

func access-is-temp(copy Int access)->(var Bool is-temp)
    is-temp := access-is-var(copy access) or access = Access.TEMP


~~~ Operator precedence ~~~
enum Precedence
    BNOT
    ARITHMETIC
    RELATIONAL
    NOT
    LOGICAL
    ASSIGN


struct Global
    ~~~ stores all user defined nodes ~~~
    var SyntaxTreeRoot root

    ~~~ stores all built-in nodes ~~~
    var GlobalNodes builtins

    ~~~ stores all language operators ~~~
    var List{Operator} operators

    ~~~ maps all global-scope nodes ~~~
    var ModuleMembers global-module

    ~~~ maps all language operators ~~~
    var NameMap{Operator} operator-map

    ~~~ maps all modules ~~~
    var NameMap{ModuleMembers} module-map

    ~~~ maps access number to its name ~~~
    owner Array{String} access-names

    ~~~ maps integer alias to its range ~~~
    var List{IntRange} int-ranges
    var NameMap{IntRange} int-map

    ~~~ the currently parsed input file object ~~~
    owner File input-file

    ~~~ the currently written output file object ~~~
    owner File output-file

    ~~~ holds the name of the input-file and the line number of the code
    currently been worked with ~~~
    user String input-file-name
    user ModuleMembers current-module
    var Int input-file-index
    var Int line-number

    ~~~ test functions list ~~~
    var NameMap{SyntaxTreeTestFunction} test-functions
    owner String tested-module
    var Bool is-new-mocked
    var Bool is-delete-mocked

    ~~~ input reading state ~~~
    owner String input-buffer
    var Char last-char
    var Int input-spaces
    var Bool got-new-line
    var Bool save-input
    var Bool file-ended

    ~~~ built-in types ~~~
    user TypeData type-char
    user TypeData type-byte
    user TypeData type-bool
    user TypeData type-int
    user TypeData type-empty
    user TypeData type-func
    user TypeData type-ref
    user TypeData type-array
    user TypeData type-string
    user TypeData type-buffer
    user TypeData type-type
    user TypeData type-base
    user TypeData type-generic
    user TypeData type-module
    user TypeData type-file
    user TypeData type-file-read-text
    user TypeData type-file-read-binary
    user TypeData type-file-write-text
    user TypeData type-file-write-binary
    user TypeData type-file-read-write-text
    user TypeData type-file-read-write-binary
    user TypeData type-pointer
    user TypeData type-c-char

    ~~~ referenced built-in functions ~~~
    user SyntaxTreeFunction string-copy-function
    user SyntaxTreeFunction buffer-copy-function

    func new()
        glob := self
        self.builtins.new()
        self.root.new()
        self.input-buffer := String{256}()
        self.global-module.new(user _)
        self.init-operator-map()
        self.init-access-names()
        self.init-int-map()
        self.init-builtin-types()

    func init-access-names()
        self.access-names := Array{Access.length:String{16}}()
        self.access-names[Access.UNDEFINED].new(user "{undefined}")
        self.access-names[Access.VAR].new(user "var")
        self.access-names[Access.COPY].new(user "copy")
        self.access-names[Access.OWNER].new(user "owner")
        self.access-names[Access.USER].new(user "user")
        self.access-names[Access.TEMP].new(user "temp")
        self.access-names[Access.STRONG].new(user "strong")
        self.access-names[Access.WEAK].new(user "weak")
        self.access-names[Access.S-VAR].new(user "s-var")
        self.access-names[Access.SHARED].new(user "shared")

    func init-operator-map()
        self.add-operator(
                user "bnot",
                user "~",
                copy Precedence.BNOT,
                copy 0,
                copy binary-not-operator-factory,
                copy _)
        self.add-operator-copy(
                user "+",
                copy Precedence.ARITHMETIC,
                copy 0,
                copy _,
                copy addition-operator-factory)
        self.add-operator-copy(
                user "-",
                copy Precedence.ARITHMETIC,
                copy 0,
                copy negation-operator-factory,
                copy subtraction-operator-factory)
        self.add-operator-copy(
                user "*",
                copy Precedence.ARITHMETIC,
                copy 1,
                copy _,
                copy multiplication-operator-factory)
        self.add-operator(
                user "div",
                user "/",
                copy Precedence.ARITHMETIC,
                copy 1,
                copy _,
                copy division-operator-factory)
        self.add-operator(
                user "mod",
                user "%",
                copy Precedence.ARITHMETIC,
                copy 1,
                copy _,
                copy modulo-operator-factory)
        self.add-operator(
                user "bor",
                user "|",
                copy Precedence.ARITHMETIC,
                copy 2,
                copy _,
                copy generic-binary-operator-factory)
        self.add-operator(
                user "band",
                user "&",
                copy Precedence.ARITHMETIC,
                copy 2,
                copy _,
                copy generic-binary-operator-factory)
        self.add-operator(
                user "xor",
                user "^",
                copy Precedence.ARITHMETIC,
                copy 2,
                copy _,
                copy generic-binary-operator-factory)
        self.add-operator(
                user "shr",
                user ">>",
                copy Precedence.ARITHMETIC,
                copy 2,
                copy _,
                copy generic-binary-operator-factory)
        self.add-operator(
                user "shl",
                user "<<",
                copy Precedence.ARITHMETIC,
                copy 2,
                copy _,
                copy generic-binary-operator-factory)
        self.add-operator(
                user "=",
                user "==",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy equalizer-operator-factory)
        self.add-operator(
                user "<>",
                user "!=",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy equalizer-operator-factory)
        self.add-operator-copy(
                user ">",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy relational-operator-factory)
        self.add-operator-copy(
                user "<",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy relational-operator-factory)
        self.add-operator-copy(
                user ">=",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy relational-operator-factory)
        self.add-operator-copy(
                user "<=",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy relational-operator-factory)
        self.add-operator(
                user "is",
                user "==",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy identity-operator-factory)
        self.add-operator(
                user "is-not",
                user "!=",
                copy Precedence.RELATIONAL,
                copy 0,
                copy _,
                copy identity-operator-factory)
        self.add-operator(
                user "not",
                user "!",
                copy Precedence.NOT,
                copy 0,
                copy not-operator-factory,
                copy _)
        self.add-operator(
                user "or",
                user "||",
                copy Precedence.LOGICAL,
                copy 0,
                copy _,
                copy logical-operator-factory)
        self.add-operator(
                user "and",
                user "&&",
                copy Precedence.LOGICAL,
                copy 1,
                copy _,
                copy logical-operator-factory)
        self.add-operator(
                user ":=",
                user "=",
                copy Precedence.ASSIGN,
                copy 0,
                copy _,
                copy assign-operator-factory)
        self.add-operator-copy(
                user "+=",
                copy Precedence.ASSIGN,
                copy 1,
                copy _,
                copy arithmetic-assign-operator-factory)
        self.add-operator-copy(
                user "-=",
                copy Precedence.ASSIGN,
                copy 2,
                copy _,
                copy arithmetic-assign-operator-factory)
        self.add-operator-copy(
                user "*=",
                copy Precedence.ASSIGN,
                copy 3,
                copy _,
                copy arithmetic-assign-operator-factory)
        self.add-operator(
                user ":=:",
                user "swap",
                copy Precedence.ASSIGN,
                copy 4,
                copy _,
                copy swap-operator-factory)

    func add-operator(
            user String name,
            user String c-name,
            copy Int precedence,
            copy Int group-index,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner UnaryExpression expression)} unary-factory,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner BinaryExpression expression)} binary-factory)
        new Operator operator(
                user name,
                user c-name,
                copy precedence,
                copy group-index,
                copy unary-factory,
                copy binary-factory)
        self.operator-map.add(user operator.name, user operator)
        self.operators.add(owner operator)

    func add-operator-copy(
            user String name,
            copy Int precedence,
            copy Int group-index,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner UnaryExpression expression)} unary-factory,
            copy Func{(user SyntaxTreeCode code-node, user Operator operator)->(
            owner BinaryExpression expression)} binary-factory)
        self.add-operator(
                user name,
                user name,
                copy precedence,
                copy group-index,
                copy unary-factory,
                copy binary-factory)

    func init-int-map()
        self.add-int-alias(user "Uint8", user _, user "255")
        self.add-int-alias(user "Uint16", user _, user "65535")
        self.add-int-alias(user "Uint32", user _, user "4294967295")
        self.add-int-alias(user "Uint64", user _, user "18446744073709551615")
        self.add-int-alias(user "Sint8", user "-128", user "127")
        self.add-int-alias(user "Sint16", user "-32768", user "32767")
        self.add-int-alias(
                user "Sint32", user "-2147483648", user "2147483647")
        self.add-int-alias(
                user "Sint64",
                user "-9223372036854775808",
                user "9223372036854775807")

    func add-int-alias(
            user String name, user String min-value, user String max-value)
        new IntRange int-range
        int-range.min-value := Long()
        if min-value?
            int-range.min-value.parse(user min-value)
        int-range.max-value := Long()
        int-range.max-value.parse(user max-value)
        string-new-copy(user name)->(owner int-range.name)
        self.int-map.add(user int-range.name, user int-range)
        self.int-ranges.add(owner int-range)

    func init-builtin-types()
        self.init-primitive-types()
        self.init-slice-types()
        self.init-file-types()
        self.init-sys-module()
        self.init-cdef-module()

    func init-primitive-types()
        self.add-global-type(user "Char", copy true)->(user self.type-char)
        self.type-char.is-int-like := true
        self.add-global-type(user "Byte", copy true)->(user self.type-byte)
        self.type-byte.is-int-like := true
        self.add-global-type(user "Bool", copy true)->(user self.type-bool)
        self.add-global-type(user "Int", copy true)->(user self.type-int)
        self.type-int.is-int-like := true
        
        self.add-global-type(user "Empty Symbol", copy false)->(
                user self.type-empty)
        self.add-global-type(user "Func", copy true)->(user self.type-func)
        self.add-global-type(user "Ref", copy true)->(user self.type-ref)
        self.add-global-type(user "Type Name", copy false)->(
                user self.type-type)
        self.add-global-type(user "Base Symbol", copy false)->(
                user self.type-base)
        self.add-global-type(user "Generic Type", copy false)->(
                user self.type-generic)
        self.type-generic.is-dynamic := true
        self.add-global-type(user "Module Name", copy false)->(
                user self.type-module)

        ; Bool
        self.add-builtin-global-variable(
                user self.global-module,
                user self.type-bool,
                user _,
                user "true")
        self.add-builtin-global-variable(
                user self.global-module,
                user self.type-bool,
                user _,
                user "false")

    func init-slice-types()
        self.add-global-type(user "Array", copy false)->(user self.type-array)
        self.add-global-type(user "String", copy false)->(user self.type-string)
        self.add-global-type(user "Buffer", copy false)->(user self.type-buffer)
        
        user SyntaxTreeFunction function
        ; Int
        self.add-builtin-method(
                user self.type-int, user "str", copy true, copy Access.COPY)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "str")
        
        ; Array
        ; self.add-builtin-field(
        ;     user self.type-array, user "length", user self.type-int, user _)
        self.add-builtin-method(
                user self.type-array,
                user "length",
                copy false,
                copy Access.USER)->(
                user function)
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "length")

        ; String
        self.add-sequence-methods(
                user self.type-string, user self.type-char)->(
                user self.string-copy-function)

        self.add-builtin-method(
                user self.type-string,
                user "concat-int",
                copy true,
                copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "number")

        ; Buffer
        self.add-sequence-methods(
                user self.type-buffer, user self.type-byte)->(
                user self.buffer-copy-function)
    
    func add-sequence-methods(
            user TypeData seq-type, user TypeData value-type)->(
            user SyntaxTreeFunction copy-function)
        user SyntaxTreeFunction function
        
        ; self.add-builtin-field(
        ;         user seq-type,
        ;         user "length",
        ;         user self.type-int,
        ;         user _)
        ; self.add-builtin-field(
        ;         user seq-type,
        ;         user "max-length",
        ;         user self.type-int,
        ;         user _)
        
        self.add-builtin-method(
                user seq-type, user "length", copy false, copy Access.USER)->(
                user function)
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "length")

        self.add-builtin-method(
                user seq-type,
                user "max-length",
                copy false,
                copy Access.USER)->(
                user function)
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "max-length")

        self.add-builtin-method(
                user seq-type,
                user "copy",
                copy true,
                copy Access.USER)->(
                user function)
        self.add-builtin-array-argument(
                user function,
                user value-type,
                copy false,
                copy Access.USER,
                user "other")
        copy-function := function

        self.add-builtin-method(
                user seq-type, user "clear", copy false, copy Access.USER)->(
                user function)

        self.add-builtin-method(
                user seq-type, user "equal", copy false, copy Access.USER)->(
                user function)
        self.add-builtin-array-argument(
                user function,
                user value-type,
                copy false,
                copy Access.USER,
                user "pattern")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-bool,
                user "equal")

        self.add-builtin-method(
                user seq-type, user "get", copy true, copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "index")
        self.add-builtin-output(
                user function, copy Access.VAR, user value-type, user "value")

        self.add-builtin-method(
                user seq-type, user "set", copy true, copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "index")
        self.add-builtin-parameter(
                user function, copy Access.COPY, user value-type, user "value")

        self.add-builtin-method(
                user seq-type, user "append", copy true, copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function, copy Access.COPY, user value-type, user "value")

        self.add-builtin-method(
                user seq-type, user "concat", copy true, copy Access.USER)->(
                user function)
        self.add-builtin-array-argument(
                user function,
                user value-type,
                copy false,
                copy Access.USER,
                user "other")

        self.add-builtin-method(
                user seq-type, user "find", copy false, copy Access.USER)->(
                user function)
        self.add-builtin-array-argument(
                user function,
                user value-type,
                copy false,
                copy Access.USER,
                user "pattern")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "index")

        self.add-builtin-method(
                user seq-type, user "has", copy false, copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function, copy Access.COPY, user value-type, user "value")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-bool,
                user "equal")

    func init-file-types()
        self.add-global-type(user "File", copy false)->(user self.type-file)
        self.add-global-type(user "FileReadText", copy false)->(
                user self.type-file-read-text)
        self.add-global-type(user "FileReadBinary", copy false)->(
                user self.type-file-read-binary)
        self.add-global-type(user "FileWriteText", copy false)->(
                user self.type-file-write-text)
        self.add-global-type(user "FileWriteBinary", copy false)->(
                user self.type-file-write-binary)
        self.add-global-type(user "FileReadWriteText", copy false)->(
                user self.type-file-read-write-text)
        self.add-global-type(user "FileReadWriteBinary", copy false)->(
                user self.type-file-read-write-binary)
        
        self.add-file-methods(
                user self.type-file,
                copy false,
                copy false,
                user _)
        self.add-file-methods(
                user self.type-file-read-text,
                copy true,
                copy false,
                user self.type-char)
        self.add-file-methods(
                user self.type-file-read-binary,
                copy true,
                copy false,
                user self.type-byte)
        self.add-file-methods(
                user self.type-file-write-text,
                copy false,
                copy true,
                user self.type-char)
        self.add-file-methods(
                user self.type-file-write-binary,
                copy false,
                copy true,
                user self.type-byte)
        self.add-file-methods(
                user self.type-file-read-write-text,
                copy true,
                copy true,
                user self.type-char)
        self.add-file-methods(
                user self.type-file-read-write-binary,
                copy true,
                copy true,
                user self.type-byte)

    func add-file-methods(
            user TypeData file-type,
            copy Bool is-read,
            copy Bool is-write,
            user TypeData value-type)
        user SyntaxTreeFunction function
        
        self.add-builtin-field(
                user file-type, user _, user self.type-ref, user _)
        
        if value-type?
            self.add-builtin-method(
                    user file-type, user "new", copy true, copy Access.TEMP)->(
                    user function)
            self.add-builtin-parameter(
                    user function,
                    copy Access.USER,
                    user self.type-string,
                    user "name")
        if is-write
            self.add-builtin-parameter(
                    user function,
                    copy Access.COPY,
                    user self.type-bool,
                    user "append")
            if is-read
                self.add-builtin-parameter(
                        user function,
                        copy Access.COPY,
                        user self.type-bool,
                        user "exist")
        file-type.constructor := function

        self.add-builtin-method(
                user file-type, user "close", copy true, copy Access.USER)->(
                user function)

        self.add-builtin-method(
                user file-type, user "tell", copy true, copy Access.USER)->(
                user function)
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "offset")

        self.add-builtin-method(
                user file-type,
                user "seek-set",
                copy true,
                copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "offset")

        self.add-builtin-method(
                user file-type,
                user "seek-cur",
                copy true,
                copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "offset")

        self.add-builtin-method(
                user file-type,
                user "seek-end",
                copy true,
                copy Access.USER)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "offset")

        self.add-builtin-method(
                user file-type, user "flush", copy true, copy Access.USER)->(
                user function)
        
        if is-read
            self.add-builtin-method(
                    user file-type, user "get", copy true, copy Access.USER)->(
                    user function)
            self.add-builtin-output(
                    user function,
                    copy Access.VAR,
                    user value-type,
                    user "value")
            self.add-builtin-output(
                    user function,
                    copy Access.VAR,
                    user self.type-bool,
                    user "is-eof")
            if value-type is glob.type-char
                self.add-builtin-method(
                        user file-type,
                        user "getline",
                        copy true,
                        copy Access.USER)->(
                        user function)
                self.add-builtin-parameter(
                        user function,
                        copy Access.USER,
                        user self.type-string,
                        user "line")
                self.add-builtin-output(
                        user function,
                        copy Access.VAR,
                        user self.type-bool,
                        user "is-eof")
            else
                self.add-builtin-method(
                        user file-type,
                        user "read",
                        copy true,
                        copy Access.USER)->(
                        user function)
                self.add-builtin-array-argument(
                        user function,
                        user value-type,
                        copy false,
                        copy Access.USER,
                        user "data")
                self.add-builtin-output(
                        user function,
                        copy Access.VAR,
                        user glob.type-int,
                        user "bytes-read")
        
        if is-write
            self.add-builtin-method(
                    user file-type, user "put", copy true, copy Access.USER)->(
                    user function)
            self.add-builtin-parameter(
                    user function,
                    copy Access.COPY,
                    user value-type,
                    user "value")

            self.add-builtin-method(
                    user file-type,
                    user "write",
                    copy true,
                    copy Access.USER)->(
                    user function)
            self.add-builtin-array-argument(
                    user function,
                    user value-type,
                    copy false,
                    copy Access.USER,
                    user "data")
            self.add-builtin-output(
                    user function,
                    copy Access.VAR,
                    user glob.type-int,
                    user "written")

    func init-sys-module()
        new ModuleMembers sys-module(user "sys")
        self.add-builtin-global-variable(
                user sys-module,
                user self.type-array,
                user self.type-string,
                user "argv")

        self.add-builtin-global-variable(
                user sys-module,
                user self.type-file-read-text,
                user _,
                user "stdin")

        self.add-builtin-global-variable(
                user sys-module,
                user self.type-file-write-text,
                user _,
                user "stdout")

        self.add-builtin-global-variable(
                user sys-module,
                user self.type-file-write-text,
                user _,
                user "stderr")

        user SyntaxTreeFunction function
        self.add-builtin-global-function(
                user sys-module, user "print", copy true)->(user function)
        self.add-builtin-array-argument(
                user function,
                user self.type-char,
                copy false,
                copy Access.USER,
                user "text")

        self.add-builtin-global-function(
                user sys-module, user "println", copy true)->(user function)
        self.add-builtin-array-argument(
                user function,
                user self.type-char,
                copy false,
                copy Access.USER,
                user "text")

        self.add-builtin-global-function(
                user sys-module, user "getchar", copy true)->(user function)
        self.add-builtin-output(
                user function, copy Access.VAR, user self.type-char, user "ch")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-bool,
                user "is-eof")

        self.add-builtin-global-function(
                user sys-module, user "getline", copy true)->(user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "line")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-bool,
                user "is-eof")

        self.add-builtin-global-function(
                user sys-module, user "exit", copy true)->(user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "status")

        self.add-builtin-global-function(
                user sys-module, user "system", copy true)->(user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "command")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-int,
                user "status")

        self.add-builtin-global-function(
                user sys-module, user "getenv", copy true)->(user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "name")
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "value")
        self.add-builtin-output(
                user function,
                copy Access.VAR,
                user self.type-bool,
                user "exists")
        
        self.root.modules.add(owner sys-module)

    func init-cdef-module()
        ; cdef types
        new ModuleMembers cdef-module(user "cdef")
        self.add-cint-type(user cdef-module, user "Char")->(
                user self.type-c-char)
        self.add-cint-type(user cdef-module, user "Uchar")
        self.add-cint-type(user cdef-module, user "Short")
        self.add-cint-type(user cdef-module, user "Ushort")
        self.add-cint-type(user cdef-module, user "Int")
        self.add-cint-type(user cdef-module, user "Uint")
        self.add-cint-type(user cdef-module, user "Long")
        self.add-cint-type(user cdef-module, user "Ulong")
        self.add-cint-type(user cdef-module, user "Size")
        self.add-cint-type(user cdef-module, user "Float")
        self.add-cint-type(user cdef-module, user "Double")
        self.add-cint-type(user cdef-module, user "LongDouble")
        self.add-cdef-type(user cdef-module, user "Pointer")->(
                user self.type-pointer)
        self.type-pointer.parameters := List{String}()
        self.type-pointer.parameters.add(owner string-new-copy(user "Pointed"))

        user SyntaxTreeFunction function
        user TypeInstance argument-type-instance

        self.add-builtin-method(
                user self.type-pointer,
                user "set-point-to",
                copy false,
                copy Access.COPY)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.VAR,
                user self.type-generic,
                user "pointed")
        function.arguments.parameters.last.item.get-type-instance()->(
                user argument-type-instance)
        string-new-copy(user self.type-pointer.parameters.first.item)->(
                owner argument-type-instance.name)

        self.add-builtin-method(
                user self.type-pointer,
                user "set-from-ref",
                copy false,
                copy Access.COPY)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-generic,
                user "ref")
        function.arguments.parameters.last.item.get-type-instance()->(
                user argument-type-instance)
        string-new-copy(user self.type-pointer.parameters.first.item)->(
                owner argument-type-instance.name)

        self.add-builtin-method(
                user self.type-pointer,
                user "set-from-array",
                copy false,
                copy Access.COPY)->(
                user function)
        self.add-builtin-argument(
                user function,
                user function.arguments.parameters,
                copy false,
                copy Access.USER,
                user self.type-array,
                user self.type-generic,
                user "array")
        function.arguments.parameters.last.item.get-type-instance()->(
                user argument-type-instance)
        string-new-copy(user self.type-pointer.parameters.first.item)->(
                owner argument-type-instance.parameters.first.item.name)

        self.add-builtin-method-native(
                user self.type-pointer,
                user "get-pointed-at",
                copy false,
                user "cdef_M_Pointer_get_pointed_at",
                copy Access.COPY)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "index")
        self.add-builtin-output(
                user function,
                copy Access.TEMP,
                user self.type-generic,
                user "pointed")
        function.arguments.outputs.last.item.get-type-instance()->(
                user argument-type-instance)
        argument-type-instance.conditional := false
        string-new-copy(user self.type-pointer.parameters.first.item)->(
                owner argument-type-instance.name)

        self.add-builtin-method-native(
                user self.type-pointer,
                user "get-ref-at",
                copy false,
                user "cdef_M_Pointer_get_ref_at",
                copy Access.COPY)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "index")
        self.add-builtin-output(
                user function,
                copy Access.USER,
                user self.type-generic,
                user "pointed")
        function.arguments.outputs.last.item.get-type-instance()->(
                user argument-type-instance)
        argument-type-instance.conditional := false
        string-new-copy(user self.type-pointer.parameters.first.item)->(
                owner argument-type-instance.name)

        self.add-builtin-global-function(
                user cdef-module, user "copy-to-string", copy true)->(
                user function)
        self.add-builtin-argument(
                user function,
                user function.arguments.parameters,
                copy false,
                copy Access.COPY,
                user self.type-pointer,
                user self.type-char,
                user "pointer")
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "target")

        self.add-builtin-global-function(
                user cdef-module, user "set-null-term-length", copy false)->(
                user function)
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-string,
                user "target")

        self.add-builtin-global-function(
                user cdef-module, user "copy-to-buffer", copy true)->(
                user function)
        self.add-builtin-argument(
                user function,
                user function.arguments.parameters,
                copy false,
                copy Access.COPY,
                user self.type-pointer,
                user self.type-byte,
                user "pointer")
        self.add-builtin-parameter(
                user function,
                copy Access.COPY,
                user self.type-int,
                user "length")
        self.add-builtin-parameter(
                user function,
                copy Access.USER,
                user self.type-buffer,
                user "target")
        
        self.root.modules.add(owner cdef-module)
    
    func add-global-type(user String name, copy Bool is-primitive)->(
            user TypeData type-data)
        self.add-builtin-type(
                user self.global-module, user name, copy is-primitive)->(
                user type-data)

    func add-cint-type(user ModuleMembers cdef-module, user String name)->(
            user TypeData type-data)
        self.add-cdef-type(user cdef-module, user name)->(user type-data)
        type-data.is-int-like := true

    func add-cdef-type(user ModuleMembers cdef-module, user String name)->(
            user TypeData type-data)
        self.add-builtin-type(user cdef-module, user name, copy true)->(
                user type-data)
        type-data.my-module := cdef-module

    func add-builtin-type(
            user ModuleMembers module,
            user String name,
            copy Bool is-primitive)->(
            user TypeData type-data)
        new TypeData new-type
        type-data := new-type
        string-new-copy(user name)->(owner new-type.name)
        new-type.is-primitive := is-primitive
        new-type.is-ordered := true
        module.type-map.add(user new-type.name, user new-type)
        self.builtins.types.add(owner new-type)

    func add-builtin-global-variable(
            user ModuleMembers module,
            user TypeData variable-type,
            user TypeData variable-subtype,
            user String name)
        self.add-builtin-variable(
                user name,
                user variable-type,
                user variable-subtype,
                user _,
                user self.builtins)
        user SyntaxTreeVariable variable(user self.builtins.variables.last.item)
        if module is-not self.global-module
            variable.my-module := module
        module.variable-map.add(user variable.name, user variable)

    func add-builtin-field(
            user TypeData builtin-type,
            user String name,
            user TypeData field-type,
            user TypeData field-subtype)
        self.add-builtin-variable(
                user name,
                user field-type,
                user field-subtype,
                user builtin-type,
                user builtin-type)

    func add-builtin-variable(
            user String name,
            user TypeData variable-type,
            user TypeData variable-subtype,
            user TypeData parent-type,
            user SyntaxTreeBranch branch)
        new SyntaxTreeVariable variable(user _)
        string-new-copy(user name)->(owner variable.name)
        if variable-type.is-primitive
            variable.access := Access.VAR
        else
            variable.access := Access.STRONG
        variable-type.new-type-instance()->(owner variable.type-instance)
        if variable-subtype?
            variable.type-instance.add-subtype-copy(user variable-subtype)
        variable.parent-type := parent-type
        branch.variables.add(owner variable)

    func add-builtin-global-function(
            user ModuleMembers module,
            user String name,
            copy Bool has-error)->(
            user SyntaxTreeFunction function)
        self.add-builtin-function(
                user name,
                copy has-error,
                user _,
                user _,
                user self.builtins)->(
                user function)
        if module is-not self.global-module
            function.my-module := module
        module.function-map.add(user function.name, user function)

    func add-builtin-method(
            user TypeData builtin-type,
            user String name,
            copy Bool has-error,
            copy Int access)->(
            user SyntaxTreeFunction function)
        self.add-builtin-method-native(
                user builtin-type,
                user name,
                copy has-error,
                user _,
                copy access)->(
                user function)

    func add-builtin-method-native(
            user TypeData builtin-type,
            user String name,
            copy Bool has-error,
            user String native-name,
            copy Int access)->(
            user SyntaxTreeFunction function)
        self.add-builtin-function(
                user name,
                copy has-error,
                user native-name,
                user builtin-type,
                user builtin-type)->(
                user function)
        self.add-builtin-parameter(
                user function, copy access, user builtin-type, user "self")
        user Argument self-param(user function.arguments.parameters.first.item)
        self-param.is-native := native-name?
        function.self-access := self-param.access
        function.self-conditional := self-param.get-type-instance().conditional

    func add-builtin-function(
            user String name,
            copy Bool has-error,
            user String native-name,
            user TypeData parent-type,
            user SyntaxTreeNamespace namespace)->(
            user SyntaxTreeFunction out-function)
        owner SyntaxTreeFunction function
        if native-name?
            new NativeFunction native-function
            native-function.cname := NativeText(user native-name)
            function := native-function
        else
            function := SyntaxTreeFunction()
        function.arguments.has-error := has-error
        out-function := function
        string-new-copy(user name)->(owner function.name)
        function.parent-type := parent-type
        function.delete-group-builder.done()
        namespace.functions.add(owner function)

    func add-builtin-parameter(
            user SyntaxTreeFunction function,
            copy Int access,
            user TypeData parameter-type,
            user String name)
        self.add-builtin-argument(
                user function,
                user function.arguments.parameters,
                copy false,
                copy access,
                user parameter-type,
                user _,
                user name)

    func add-builtin-array-argument(
            user SyntaxTreeFunction function,
            user TypeData subtype,
            copy Bool is-output,
            copy Int access,
            user String name)
        self.add-builtin-argument(
                user function,
                user function.arguments.parameters,
                copy is-output,
                copy access,
                user self.type-array,
                user subtype,
                user name)

    func add-builtin-output(
            user SyntaxTreeFunction function,
            copy Int access,
            user TypeData output-type,
            user String name)
        self.add-builtin-argument(
                user function,
                user function.arguments.outputs,
                copy true,
                copy access,
                user output-type,
                user _,
                user name)

    func add-builtin-argument(
            user SyntaxTreeFunction function,
            user List{Argument} argument-list,
            copy Bool is-output,
            copy Int access,
            user TypeData argument-type,
            user TypeData argument-subtype,
            user String name)
        new DeclarationArgument argument
        argument.access := access
        argument.is-output := is-output
        if function.arguments.parameters.first?
            argument.is-native :=
                    function.arguments.parameters.first.item.is-native
        argument.variable := SyntaxTreeVariable(user _)
        string-new-copy(user name)->(owner argument.variable.name)
        argument.variable.access := access
        argument-type.new-type-instance()->(
                owner argument.variable.type-instance)
        if argument-subtype?
            argument.variable.type-instance.parameters := List{TypeInstance}()
            argument.variable.type-instance.parameters.add(
                    owner argument-subtype.new-type-instance())
        if is-output and not argument-type.is-primitive
            argument.variable.type-instance.conditional := true
        ; if access = Access.OWNER or access = Access.STRONG
        ;     function.delete-group.add-deleting(
        ;             user argument.variable.type-instance)
        argument-list.add(owner argument)

    func find-type(user String name)->(user TypeData type-data)
        if self.current-module?
            if self.current-module.type-map.find(user name)->(user type-data)?
                return
        self.global-module.type-map.find(user name)->(user type-data)

    func find-variable(user String name)->(user SyntaxTreeVariable variable)
        if self.current-module?
            if self.current-module.variable-map.find(user name)->(
                    user variable)?
                return
        self.global-module.variable-map.find(user name)->(user variable)

    func find-function(user String name)->(user SyntaxTreeFunction function)
        if self.current-module.function-map.find(user name)->(user function)?
            return
        self.global-module.function-map.find(user name)->(user function)

user Global glob
